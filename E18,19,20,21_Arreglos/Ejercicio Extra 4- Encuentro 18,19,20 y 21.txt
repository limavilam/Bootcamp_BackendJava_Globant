//Crear un vector que contenga 100 notas de 100 supuestos estudiantes, con valores entre 0 y
//20 generadas aleatoriamente mediante el uso de la función azar() o aleatorio() de PseInt.
//Luego, de acuerdo a las notas contenidas, el programa debe indicar cuántos estudiantes son:
//a)! Deficientes 0-5
//b)! Regulares 6-10
//c)! Buenos 11-15
//d)! Excelentes 16-20

Algoritmo notasEstudiantes
	
	Definir vector,N, i Como Entero
	Definir contadorDeficientes, contadorRegulares, contadorBuenos, contadorExcelentes Como Entero
	
	N = 100
	
	Dimension vector[N]
	
	contadorDeficientes <- 0
	contadorRegulares <- 0
	contadorBuenos <- 0
	contadorExcelentes <- 0
	
	Para i <- 0 Hasta 99 Con Paso 1 Hacer
		vector[i] <- Aleatorio(0,20)	
		
		Segun vector[i] Hacer
			0,1,2,3,4,5:
				contadorDeficientes <- contadorDeficientes + 1
			6,7,8,9,10:
				contadorRegulares <- contadorRegulares + 1
			11,12,13,14,15:
				contadorBuenos <- contadorBuenos + 1
			16,17,18,19,20:
				contadorExcelentes <- contadorExcelentes + 1
		FinSegun
	FinPara
	
	Escribir "Cantidad de estudiantes por clasificación:"
	Escribir "Deficientes: ", contadorDeficientes
	Escribir "Regulares: ", contadorRegulares
	Escribir "Buenos: ", contadorBuenos
	Escribir "Excelentes: ", contadorExcelentes
FinAlgoritmo
