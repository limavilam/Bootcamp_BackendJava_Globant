//Disponemos de un vector unidimensional de 20 elementos de tipo carácter. Se pide
//desarrollar un programa que:
//a) Pida una frase al usuario y luego ingrese la frase dentro del arreglo letra por letra.
//Ayuda: utilizar la función Subcadena de PSeInt.
//b) Una vez completado lo anterior, pedirle al usuario un carácter cualquiera y una
//posición dentro del arreglo, y el programa debe intentar ingresar el carácter en la
//posición indicada, si es que hay lugar (es decir la posición está vacía o es un espacio
//en blanco). De ser posible debe mostrar el vector con la frase y el carácter ingresado,
//de lo contrario debe darle un mensaje al usuario de que esa posición estaba ocupada.

Algoritmo fraseEspacios
	
	//variables del primer punto
	Definir vector, frase Como Caracter
	
	//Variables del segundo punto
	Definir posicion Como Real
	Definir letra Como Caracter
		
	Dimension vector[20]
	
	Escribir "Ingrese una frase:"
	Leer frase
	
	
	Escribir "Ingrese un caracter:" 
	Leer letra
	Escribir "Ingrese una posición(0-19) para agregarlo en el vector:" 
	Leer posicion
	
	rellenarVector(vector,frase)
	agregarCaracter(vector,posicion,letra)
	mostrarVector(vector)
		
FinAlgoritmo

//Rellenar vector
SubProceso rellenarVector(vector,frase)
	Definir i Como Entero
	
	Para i=0 Hasta 19 Hacer
		vector[i]=Subcadena(frase,i,i)
	FinPara	
FinSubProceso

//Agregar caracter nuevo
SubProceso agregarCaracter (vector,posicion,letra)
	Si (vector[posicion] = "" O vector[posicion] = " ") Entonces
        vector[posicion] <- letra
        Escribir "El carácter fue ingresado correctamente en la posición ", posicion
    Sino
        Escribir "La posición indicada está ocupada"
    FinSi
FinSubProceso

//Mostrar el vector
SubProceso mostrarVector (vector)
	Definir i Como Entero
	Escribir "El vector con la frase y el carácter ingresado es:"
    Para i<-0 Hasta 19 Con Paso 1 Hacer
        Escribir "[" vector[i] "]" Sin Saltar		
    FinPara
	Escribir " " 
FinSubProceso

