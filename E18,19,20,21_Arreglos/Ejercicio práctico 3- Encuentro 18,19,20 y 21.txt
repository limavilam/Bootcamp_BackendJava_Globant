//Realizar un programa que rellene un vector de tamaño N, con valores ingresados por el
//usuario. A continuación, se debe buscar un elemento dentro del arreglo (el número a buscar
//también debe ser ingresado por el usuario). El programa debe indicar la posición donde se
//encuentra el valor. En caso que el número se encuentre repetido dentro del arreglo se deben
//imprimir todas las posiciones donde se encuentra ese valor.
//Finalmente, en caso que el número a buscar no está adentro del arreglo se debe mostrar un
//mensaje.

Algoritmo arrays
	
	Definir vector, elemento, i, repeticionesElemento , N Como Entero
	Escribir "Ingrese el número de datos:"
	Leer N
	Dimension vector[N]
	
	//Bucle para la asignación de la posición del número dentro del arreglo, no olvidar que el subíndice se define como N-1.
	Para i <- 0 Hasta N-1 Con Paso 1 Hacer
		Escribir "Ingrese el elemento ", i+1, " del arreglo:"
		Leer vector[i]
	FinPara
	
	Escribir "Ingrese el número a buscar:"
	Leer elemento
	repeticionesElemento <- 0
	
	Para i <- 0 Hasta N-1 Con Paso 1 Hacer
		Si vector[i] = elemento Entonces
			//La posición del número está en i, podría sumarle i+1 para poder visualizar el inicio no desde cero
			Escribir "El elemento ", elemento, " se encuentra en la posición ", i, " del arreglo."
			//contador de veces que está el elemento en el arreglo. 
			repeticionesElemento  <- repeticionesElemento  + 1
		FinSi
	FinPara
	
	Si repeticionesElemento  = 0 Entonces
		Escribir "El elemento ", elemento, " no está adentro del arreglo."
	SiNo
		Escribir "El elemento ", elemento, " aparece ", repeticionesElemento , " veces en el arreglo."
	FinSi

	
FinAlgoritmo