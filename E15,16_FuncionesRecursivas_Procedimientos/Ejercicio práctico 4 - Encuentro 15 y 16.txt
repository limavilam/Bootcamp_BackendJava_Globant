// Escribir un programa que procese una secuencia de caracteres ingresada por teclado y
//terminada en punto, y luego codifique la palabra o frase ingresada de la siguiente manera:
//	cada vocal se reemplaza por el carácter que se indica en la tabla y el resto de los caracteres
//	(incluyendo a las vocales acentuadas) se mantienen sin cambios.
// a = @, e = #, i = $, o = %, u = *
// Realice un subprograma que reciba una secuencia de caracteres y retorne la codificación
//correspondiente. Utilice la estructura "según" para la transformación.
//Por ejemplo, si el usuario ingresa: Ayer, lunes, salimos a las once y 10.
//La salida del programa debería ser: @y#r, l*n#s, s@l$m%s @ l@s %nc# y 10.

Algoritmo cambioCaracteres
	
	Definir string Como Caracter
	Definir len Como Entero
		
	Escribir "Ingrese una cadena: "
	Leer string
	len <- Longitud(string)-1
	
	//Devuelvo la cadena ingresada.
	Mientras Subcadena(string,len,len) <> "." Hacer
		
		Escribir "Incorrecto, ingrese una cadena que termine en .(punto) :"
		Leer string
		len <- Longitud(string)-1
		
	Fin Mientras	
	
	codificacion(string)
	Escribir "La nueva cadena ahora es:", string
	
FinAlgoritmo

SubProceso codificacion(string Por Referencia)
	
	Definir len Como Entero
	Definir i Como Entero
	Definir nuevaCadena Como Caracter
	
	//inicialización de la nueva cadena donde voy a almacenar la nueva cadena.
	nuevaCadena <- ""
	len <- Longitud(string)-1
	
	Para i<-0 Hasta len Hacer
		
		Segun minusculas(subcadena(string,i,i)) Hacer
			"a":
				nuevaCadena = Concatenar(nuevaCadena,"@")
			"e":
				nuevaCadena = Concatenar(nuevaCadena,"#")
			"i":
				nuevaCadena = Concatenar(nuevaCadena,"$")
			"o":
				nuevaCadena = Concatenar(nuevaCadena,"%")
			"u":
				nuevaCadena = Concatenar(nuevaCadena,"*")
			De Otro Modo:
				nuevaCadena = Concatenar(nuevaCadena,Subcadena(string,i,i))
		FinSegun		
	Fin Para
	
	// nuevaCadena = string, si coloco como si la nueva cadena fuera string, regreso a la cadena original.
	string = nuevaCadena
FinSubProceso
	